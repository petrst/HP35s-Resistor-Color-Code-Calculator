Z001    LBL Z
Z002    0
Z003    STO R               // Reset X register to 0 which indicates
                            // no color was selected
Z004    CLx
Z005    SF10
Z006    1BLK 2BWN 3RED      // Display 1st part of menu and wait user
                            // to put selction on stack by pressing
                            // numebr and [R/S]
Z007    x=0?                // No color selected? 
Z008    4ONG 5YLW 6GRN      //   -> diplay next part of menu
Z009    x=0?
Z010    7BLU 8VLT 9GRY
Z011    CF10
Z012    x=0?
Z013    GTO Z021            // Nothing was selected in all 3 menus
                            // Go to calculation                            
Z014    1               
Z015    -                   // Decrement the selected color by 1
                            // The menu item number is actually value 
                            // corresponding to color + 1 (so that 0 can be used
                            // to indicate no selection or end ef calculation)
                            
Z016    x<> R               // Swap with R register (accumulator)

Z017    10
Z018    ×                   // New band was entered. The magnitude for current
                            // must be increased (*10) ...
Z019    STO+ R              // ... and entered value is added R(N)=R(N-1)*10 + X

Z020    GTO Z004            // Go ask for next band color

Z021    RCL R               // Final calculation - last accumulated digit
                            // must be reintrepreted as exponent of multiplier
                            // R = 223  --> 22 x 10^3
                             
Z022    10                  // Separate the integer part
Z023    ÷
Z024    IP                  

Z025    RCL R               // Take the decimal part a multiply by 10
Z026    10                  // this is exponent of multplier
Z027    ÷
Z028    FP
Z029    10                  
Z030    ×

Z031    10                  // Calculate multipler 10^exp
Z032    x<>y
Z033    y^x

Z034    ×                   // Calculate resistance value as
                            // integer part (from Z024) x multiplier (from Z033)
Z035    ENG 3
Z036    RTN
